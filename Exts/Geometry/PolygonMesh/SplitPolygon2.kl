
//  ポリゴンのエッジ分割とポリゴン分割を同時に行う
//  ポリゴン分割の際にエッジ分割位置～2つ目のエッジ分割位置までの間に複数の座標を追加することが出来るので、多角形ポリゴンへの分割も可能

//  分割後の新しいポリゴン生成のための情報"polygonSize"と"packedPolygonPoiIDs"を作る
function PolygonMesh.splitPolygon2GetValuesTask!(
  in Size polygonID,  
  in Size polygonIDList[],  //  Arrayの際に使用　　マージ候補の隣接ポリゴンIDがスプリット処理を行うポリゴンと重複していないかをチェックするために使用する　＝　スプリット処理を行う全てのポリゴンIDリストが必要
  in Size origPoiCnt, //  Array用にオリジナルメッシュのポリゴンポイント数を外部で管理する　分割処理中に生成される新規ポイントIDの管理で使用
  in Size polygonPointIndex1,
  in Scalar edgeSplitRatio1,
  in Size polygonPointIndex2,
  in Scalar edgeSplitRatio2,
  in Vec3 addPointsPosition[],
  in Boolean mergeNeighborPoly,

  io Size execPolyID[],
  io Vec3 preparedPoiPos[], //  Arrayの際に使用　　スプリットするエッジの端を別のスプリットエッジの端と継続させれるかをチェックするために使用
  io Size polygonSizes[], 
  io Size packedPolygonPointIndices[],
  io Size nPolygonSizes[], 
  io Size nPackedPolygonPointIndices[],
  io Integer nPolyID[],
  io Size useOrigPointIds[]
  )
{

  //////////  入力データのチェック
  //  エッジ指定とレシオの値それぞれが同じの場合
  //  ポリゴンIDがメッシュに存在しない場合
  //  指定したエッジがポリゴン内に無い場合
  //  2回のエッジ分割が同じエッジを使用+追加のポイントも無くポリゴン生成に必要な３頂点が確保できない場合
  if(polygonPointIndex1 == polygonPointIndex2 && edgeSplitRatio1 == edgeSplitRatio2){
    report("!!!!!!Error!!!!!! .splitPolygon2() ::: polygonPointIndex and edgeSplitRatio are same value");
    return;
  }else if(this.polygonCount() <= polygonID){ 
    report("!!!!!!Error!!!!!! .splitPolygon2() ::: polygonID is out of lange this polygon count");
    return;
  }else if(this.getPolygonSize(polygonID) <= polygonPointIndex1 || this.getPolygonSize(polygonID) <= polygonPointIndex2){ 
    report("!!!!!!Error!!!!!! .splitPolygon2() ::: polygonPointIndex is out of lange this polygon point count");
    return;
  }else if(polygonPointIndex1 == polygonPointIndex2 && addPointsPosition.size() == 0){  //  
    report("!!!!!!Error!!!!!! .splitPolygon2() ::: ");
    return;
  }
  
  //////////  エッジ分割のレシオを”0～1”の範囲にクランプする
  Scalar edgeSplitRatio[2];
    edgeSplitRatio[0] = Scalar(Math_clamp(edgeSplitRatio1, 0.0, 1.0));
    edgeSplitRatio[1] = Scalar(Math_clamp(edgeSplitRatio2, 0.0, 1.0));

  //////////  リバース対応用に入力された値を新規変数に入れておく
  Size polygonPointIndex[2];
    polygonPointIndex[0] = polygonPointIndex1;
    polygonPointIndex[1] = polygonPointIndex2;

  //////////  編集前のメッシュの情報を一時保存しておく
  Size origMeshPolyCount = this.polygonCount(); //  分割後に出来上がるポリゴンのIDの取得で使用
  Size thisPoiCnt = this.pointCount();
  Size origPoi[](this.getPolygonSize(polygonID)*3);{  //  ターゲットとなるポリゴンの頂点IDを格納しておく　2回ループさせてリスト化しておく　例：4角形ポリゴンの場合=[0,2,5,8,0,2,5,8,0,2,5,8]
    for(Integer i=0; i<origPoi.size()/3; i++){
      origPoi[i] = this.getPolygonPoint(polygonID, i);
    }
    for(Integer i=0; i<origPoi.size()/3; i++){
      origPoi[origPoi.size()/3+i] = origPoi[i];
    }
    for(Integer i=0; i<origPoi.size()/3; i++){
      origPoi[origPoi.size()/3*2+i] = origPoi[i];
    }
  }
  Integer neighborPolyID[](2);  //  分割処理を行うエッジに隣接するポリゴンIDを取得しておく　隣接するポリゴンとマージする処理がONの場合のみ実行
  if(mergeNeighborPoly){
    if(edgeSplitRatio[0] != 0.0 || edgeSplitRatio[0] != 1.0){
      PolygonMeshEdge edge = this.getPolygonEdge(polygonID, polygonPointIndex[0]);
      Size rID = this.getEdgeRightPolygon(edge);
      Size lID = this.getEdgeLeftPolygon(edge);
      if(rID != polygonID && rID != 4294967295){
        neighborPolyID[0] = rID;
      }else if(lID != polygonID && lID != 4294967295){
        neighborPolyID[0] = lID;
      }else{
        neighborPolyID[0] = -1;
      }
    }else{
      neighborPolyID[0] = -1;
    }
    if(edgeSplitRatio[1] != 0.0 || edgeSplitRatio[1] != 1.0){
      PolygonMeshEdge edge = this.getPolygonEdge(polygonID, polygonPointIndex[1]);
      Size rID = this.getEdgeRightPolygon(edge);
      Size lID = this.getEdgeLeftPolygon(edge);
      if(rID != polygonID && rID != 4294967295){
        neighborPolyID[1] = rID;
      }else if(lID != polygonID && lID != 4294967295){
        neighborPolyID[1] = lID;
      }else{
        neighborPolyID[1] = -1;
      }
    }else{
      neighborPolyID[0] = -1;
    }
  }

  //////////  分割用の頂点を生成
  Integer useOrigPolyPoi[](2);  //  エッジ分割のレシオが0.0か1.0の場合は新頂点を作らずに既存の頂点を利用する　その際にどの頂点を利用するのかポリゴンポイントで格納しておくためのもの
    useOrigPolyPoi[0] = -1;
    useOrigPolyPoi[1] = -1;
  Vec3 edgeSplitPos[2]; //  エッジ分割を行うポジション値を格納しておくためのもの
  Size addPointCount = addPointsPosition.size();//  合計で何個の頂点を追加することになるかをカウントするためのもの　このメソッドに入力されている追加頂点位置”addPointsPosition”の個数をあらかじめカウントしておく
    
  //  レシオが0.0の場合　"useOrigPolyPoi"に"polygonPointIndex"を格納
  //  レシオが1.0の場合　"useOrigPolyPoi"に"polygonPointIndex+1"を格納　ただしポリゴンの頂点数を超える数字になる場合は0番からリセットしておく
  //  どちらでもない場合はスプリット位置を"edgeSplitPos"に格納し、"addPointCount"のカウントを追加しておく
  if(edgeSplitRatio[0] == 0){
    useOrigPolyPoi[0] = polygonPointIndex[0];
    useOrigPointIds.push( this.getPolygonPoint(polygonID, useOrigPolyPoi[0]) );
  }else if(edgeSplitRatio[0] == 1.0){
    useOrigPolyPoi[0] = polygonPointIndex[0]+1;
    useOrigPointIds.push( this.getPolygonPoint(polygonID, useOrigPolyPoi[0]) );
  }else{
    edgeSplitPos[0] = this.getPointPosition(origPoi[polygonPointIndex[0]]).linearInterpolate(this.getPointPosition(origPoi[polygonPointIndex[0]+1]), edgeSplitRatio[0]);
    addPointCount += 1;
  }
  if(edgeSplitRatio[1] == 0){
    useOrigPolyPoi[1] = polygonPointIndex[1];
    useOrigPointIds.push( this.getPolygonPoint(polygonID, useOrigPolyPoi[1]) );
  }else if(edgeSplitRatio[1] == 1.0){
    useOrigPolyPoi[1] = polygonPointIndex[1]+1;
    useOrigPointIds.push( this.getPolygonPoint(polygonID, useOrigPolyPoi[1]) );
  }else{
    edgeSplitPos[1] = this.getPointPosition(origPoi[polygonPointIndex[1]]).linearInterpolate(this.getPointPosition(origPoi[polygonPointIndex[1]+1]), edgeSplitRatio[1]);
    addPointCount += 1;
  }
  if(useOrigPolyPoi[0] != -1 && useOrigPolyPoi[1] != -1 && origPoi[useOrigPolyPoi[0]] == origPoi[useOrigPolyPoi[1]]){
    report("!!!!!!Error!!!!!! .splitPolygon2() ::: select same pointID");
    return;
  }
  //  エッジ分割と同じポジション値がpreparePos[]内に無いか検索する
  Integer usePreparePosID[2];
    usePreparePosID[0] = -1;
    usePreparePosID[1] = -1;
  for(Integer i=preparedPoiPos.size()-1; 0<=i; i--){
    if(edgeSplitPos[0].almostEqual(preparedPoiPos[i])){
      addPointCount -= 1;
      usePreparePosID[0] = i;
    }
    if(edgeSplitPos[1].almostEqual(preparedPoiPos[i])){
      addPointCount -= 1;
      usePreparePosID[1] = i;
    }
  }
  this.createPoints(addPointCount); //  追加数"addPointCount"で頂点を追加する

  //////////  エッジ分割の2点がポリゴン内での左回りの順で生成されるかをチェックし、逆だった場合はポリゴンの分割で必要な素材の並び順を逆転させておく
  Boolean reverse;
  if(polygonPointIndex[1] < polygonPointIndex[0]){
    reverse = true;
  }else if(polygonPointIndex[0] == polygonPointIndex[1]){
    if(edgeSplitRatio[1] < edgeSplitRatio[0]){
      reverse = true;
    }
  }

  Vec3 addPointsPosition2[] = addPointsPosition.clone();  //  追加ポイントの情報を逆転させる場合のための受け皿
  if(reverse){  //  ポリゴンを分割するエッジのスタート・エンドに関する情報を逆転させておく
    Integer restInt;
    restInt = useOrigPolyPoi[0];
    useOrigPolyPoi[0] = useOrigPolyPoi[1];
    useOrigPolyPoi[1] = restInt;
    restInt = usePreparePosID[0];
    usePreparePosID[0] = usePreparePosID[1];
    usePreparePosID[1] = restInt;
    Vec3 restVec3 = edgeSplitPos[0];
    edgeSplitPos[0] = edgeSplitPos[1];
    edgeSplitPos[1] = restVec3;
    Scalar restScl = edgeSplitRatio[0];
    edgeSplitRatio[0] = edgeSplitRatio[1];
    edgeSplitRatio[1] = restScl;
    Size restSize = polygonPointIndex[0];
    polygonPointIndex[0] = polygonPointIndex[1];
    polygonPointIndex[1] = restSize;
    //  追加ポイントだけは配列数が予測できないため、逆転の場合の対応を別処理で行う
    addPointsPosition2.resize(addPointsPosition.size());
    for(Size i=0; i<addPointsPosition.size(); i++){
      addPointsPosition2[i] = addPointsPosition[addPointsPosition.size()-1-i];
    }
  }

  //////////  追加した分割用の頂点に位置情報を与えていく
  Integer s;
  if(useOrigPolyPoi[0] == -1  && usePreparePosID[0] == -1){  //  一つ目のエッジの分割が行われている場合は追加頂点の最初に分割位置"edgeSplitPos"の情報を渡す
    this.setPointPosition(thisPoiCnt, edgeSplitPos[0]);
    preparedPoiPos.push(edgeSplitPos[0]);
    s += 1;
  }
  for(Integer i=0; i<addPointsPosition2.size(); i++){
    this.setPointPosition(thisPoiCnt+s, addPointsPosition2[i]);
    preparedPoiPos.push(addPointsPosition2[i]);
    s += 1;
  }
  if(useOrigPolyPoi[1] == -1 && usePreparePosID[1] == -1){  //  2つ目のエッジの分割が行われている場合は追加頂点の最後に分割位置"edgeSplitPos"の情報を渡す　エッジ分割が行われていない+レシオが0.0の場合は↓頂点リスト生成のスタートIDを1減らす
    this.setPointPosition(thisPoiCnt+s, edgeSplitPos[1]);
    preparedPoiPos.push(edgeSplitPos[1]);
  }else if(edgeSplitRatio[1] == 0.0){
    s -= 1;
  }

  //////////  ポリゴンを生成する頂点番号リストを作る
  Integer ss = addPointCount-1;
  {
    //  1つ目のポリゴン　最初のポイントIDをリストに登録する　  
    if(usePreparePosID[1] != -1){  //  エッジ分割と同じポジション値がpreparePos[]にあった場合
      packedPolygonPointIndices.push(origPoiCnt+usePreparePosID[1]);
    }else if(useOrigPolyPoi[1] != -1){  //  既存の頂点だった場合
      packedPolygonPointIndices.push(origPoi[useOrigPolyPoi[1]]);
    }else{  //  新規追加した頂点の最後のポイントIDを登録
      packedPolygonPointIndices.push(thisPoiCnt+ss);
      ss -= 1;
    }
    //  1つ目のポリゴン　分割エッジの途中に頂点を追加する場合
    if(0 < addPointCount){
      if(0 <= ss){
        while(true){
          packedPolygonPointIndices.push(thisPoiCnt+ss);
          if(ss == 0){
            break;
          }
          ss -= 1;
        }
      }else{
        ss = 0;
      }
    }
    //  1つ目のポリゴン　最後のポイントIDをリストに登録する　既存の頂点やpreparePos[]内のポジションを使用する場合のみ実行（新規頂点の場合は↑のwhile内で既に登録されている）
    if(usePreparePosID[0] != -1){  //  エッジ分割と同じポジション値がpreparePos[]にあった場合
      packedPolygonPointIndices.push(origPoiCnt+usePreparePosID[0]);
    }else if(useOrigPolyPoi[0] != -1){
      packedPolygonPointIndices.push(origPoi[useOrigPolyPoi[0]]);
    } 
    
    //  1つ目のポリゴン　元のポリゴンの頂点IDを登録していく
    Scalar cnt = 1;
    Scalar cnt2 = 0;
      if(edgeSplitRatio[0] == 1.0){
        cnt += 1;
      }
      if(edgeSplitRatio[1] == 0.0){
        cnt2 = -1;
      }
    while(true){
      if(polygonPointIndex[1]+cnt2 < polygonPointIndex[0]+cnt){
        break;
      }
      packedPolygonPointIndices.push(origPoi[polygonPointIndex[0]+cnt]);
      cnt += 1;
    }
    //  1つ目のポリゴン　ポリゴンの角数を登録
    if(polygonSizes.size() == 0){
      polygonSizes.push(packedPolygonPointIndices.size());
    }else{
      Size sub;
      for(Integer i=0; i<polygonSizes.size(); i++){
        sub += polygonSizes[i];
      }
      polygonSizes.push(packedPolygonPointIndices.size() - sub);
    }
  }

  {
    //  2つ目のポリゴン　最初のポイントIDをリストに登録する
    if(usePreparePosID[1] != -1){
      s = Math_max(s-1, 0);
      ss = Math_max(ss-1, 0);
    }
    
    if(usePreparePosID[0] != -1){  //  エッジ分割と同じポジション値がpreparePos[]にあった場合
      packedPolygonPointIndices.push(origPoiCnt+usePreparePosID[0]);
    }else if(useOrigPolyPoi[0] != -1){  //  既存の頂点だった場合
      packedPolygonPointIndices.push(origPoi[useOrigPolyPoi[0]]);
    }else{  //  新規追加した頂点の最後のポイントIDを登録
      packedPolygonPointIndices.push(thisPoiCnt+ss);
      ss += 1;
    }
    //  2つ目のポリゴン 分割エッジの途中に頂点を追加する場合
    if(0 < addPointCount){
      while(true){
        if(s < ss){ //  1つめのエッジを分割+2つめは分割無し+追加頂点も無しの場合
          break;
        }
        packedPolygonPointIndices.push(thisPoiCnt+ss);
        if(s == ss){
          break;
        }
        ss += 1;
        if(edgeSplitRatio[1] == 1.0 && ss == s){  //  2つめのレシオが1.0の場合だけ1つ早くbreakを踏む
          break;
        }
      }
    }
    //  2つ目のポリゴン　最後のポイントIDをリストに登録する　既存の頂点やpreparePos[]内のポジションを使用する場合のみ実行（新規頂点の場合は↑のwhile内で既に登録されている）
    if(usePreparePosID[1] != -1){  //  エッジ分割と同じポジション値がpreparePos[]にあった場合
      packedPolygonPointIndices.push(origPoiCnt+usePreparePosID[1]);
    }else if(useOrigPolyPoi[1] != -1){
      packedPolygonPointIndices.push(origPoi[useOrigPolyPoi[1]]);
    }
    //  2つ目のポリゴン　元のポリゴンの頂点IDを登録していく
    Scalar cnt = 1;
    Scalar cnt2 = 0;
      if(edgeSplitRatio[1] == 1.0){
        cnt += 1;
      }
      if(polygonPointIndex[0] == polygonPointIndex[1]){
        cnt2 = 1;
      }
      if(edgeSplitRatio[0] != 0.0){ //  一つ目のレシオが0.0でなければcnt2=1にする　
        cnt2 = 1;
      }else{  //  レシオが0.0の場合は0のままが望ましいが、同じエッジを分割する場合は既にcnt2に1が設定されているので0に戻す
        cnt2 = 0;
      }

    while(true){
      if(origPoi[polygonPointIndex[1]+cnt] == origPoi[polygonPointIndex[0]+cnt2] && polygonPointIndex[0] != polygonPointIndex[1]){  //  異なるエッジを分割している場合
        break;
      }
      packedPolygonPointIndices.push(origPoi[polygonPointIndex[1]+cnt]);
      cnt += 1;
      if(origPoi[polygonPointIndex[1]+cnt] == origPoi[polygonPointIndex[0]+cnt2] && polygonPointIndex[0] == polygonPointIndex[1]){  //  同じエッジを分割している場合は一周ループさせるためにbreakのタイミングをpushの後にする
        break;
      }
    }
    //  2つ目のポリゴン　ポリゴンの角数を登録
    Size sub;
    for(Integer i=0; i<polygonSizes.size(); i++){
      sub += polygonSizes[i];
    }
    polygonSizes.push(packedPolygonPointIndices.size() - sub);
  }

  //////////  分割処理をしたエッジに隣り合うポリゴンにも頂点を追加する場合は隣接ポリゴンのリストも作る
  if(mergeNeighborPoly){
    //  隣接マージを行うポリゴンがスプリット処理候補のポリゴンIDリスト内に無いか検索する　　存在した場合はIDに”-1”をセットして隣接マージ処理を行わないようにする
    for(Integer i=0; i<polygonIDList.size(); i++){
      if(neighborPolyID[0] == polygonIDList[i]){
        neighborPolyID[0] = -1;
      }
      if(neighborPolyID[1] == polygonIDList[i]){
        neighborPolyID[1] = -1;
      }
    }
    //  リバース処理　
    if(reverse){
      Size restSize = neighborPolyID[0];
      neighborPolyID[0] = neighborPolyID[1];
      neighborPolyID[1] = restSize;
    }
    Boolean samePolyID = neighborPolyID[0] == neighborPolyID[1];
    Size oriSiz = nPackedPolygonPointIndices.size();
    if(neighborPolyID[0] != -1 && useOrigPolyPoi[0] == -1){    
      for(Integer i=0; i<this.getPolygonSize(neighborPolyID[0]); i++){
        Size polyID = this.getPolygonPoint(neighborPolyID[0], i);
        nPackedPolygonPointIndices.push(polyID);
        if(polyID == origPoi[polygonPointIndex[0]+1]){
          if(samePolyID){
            nPackedPolygonPointIndices.push(thisPoiCnt+s);
          }
          nPackedPolygonPointIndices.push(thisPoiCnt);
        }
      }
      nPolygonSizes.push(nPackedPolygonPointIndices.size() - oriSiz);
      oriSiz = nPackedPolygonPointIndices.size();
      nPolyID.push(neighborPolyID[0]);
    }
    if(!samePolyID && neighborPolyID[1] != -1 && useOrigPolyPoi[1] == -1){
      for(Integer i=0; i<this.getPolygonSize(neighborPolyID[1]); i++){
        Size polyID = this.getPolygonPoint(neighborPolyID[1], i);
        nPackedPolygonPointIndices.push(polyID);
        if(polyID == origPoi[polygonPointIndex[1]+1]){
          nPackedPolygonPointIndices.push(thisPoiCnt+s);
        }
      }
      nPolygonSizes.push(nPackedPolygonPointIndices.size() - oriSiz);
      nPolyID.push(neighborPolyID[1]);
    }
  }
  execPolyID.push(polygonID);
}

//  リストを整理し直してからターゲットポリゴンのスプリットと隣接ポリゴンのマージを行う
function PolygonMesh.splitPolygon2ApplayTask!(
  in Size polygonIDs[],
  in Integer nPolyID[],
  in Size polygonSizes[],
  io Size nPolygonSizes[],
  in Size packedPolygonPointIndices[],
  io Size nPackedPolygonPointIndices[]
  )
{
  if(polygonIDs.size() == 0 || polygonSizes.size() == 0 || packedPolygonPointIndices.size() == 0){
    return;
  }
  
  // delete target & neighbor polygon
  for(Integer i=0; i<nPolyID.size(); i++){
    if(nPolyID[i] != -1){
      this.deletePolygon(nPolyID[i], false, false);
    }
  }
  for(Integer i=0; i<polygonIDs.size(); i++){
    this.deletePolygon(polygonIDs[i], false, false);
  }  
  
  //  split polygon 
  for(Integer i=0; i<polygonSizes.size(); i++){
    nPolygonSizes.push(polygonSizes[i]);// merge polygon list
  }
  for(Integer i=0; i<packedPolygonPointIndices.size(); i++){
    nPackedPolygonPointIndices.push(packedPolygonPointIndices[i]);// merge polygon list
  }

  this.addPolygons(nPolygonSizes, nPackedPolygonPointIndices);
}

function PolygonMesh.splitPolygon2!(
  in Size polygonID,
  in Size polygonPointIndex1,
  in Scalar edgeSplitRatio1,
  in Size polygonPointIndex2,
  in Scalar edgeSplitRatio2,
  in Vec3 addPointsPosition[],
  in Boolean mergeNeighborPoly,
  out Size pointIds[],
  )
{
  Size polygonSizes[], packedPolygonPointIndices[], nPolygonSizes[], nPackedPolygonPointIndices[], useOrigPointIds[], dummy[], dummy2[];
  Integer nPolyID[];
  Vec3 preparePoiPos[];
  Size origPoiCnt = this.pointCount();
  this.splitPolygon2GetValuesTask(
    polygonID,
    dummy,
    this.pointCount(),
    polygonPointIndex1, 
    edgeSplitRatio1, 
    polygonPointIndex2, 
    edgeSplitRatio2, 
    addPointsPosition, 
    mergeNeighborPoly,
    dummy2,
    preparePoiPos,
    polygonSizes, 
    packedPolygonPointIndices, 
    nPolygonSizes, 
    nPackedPolygonPointIndices, 
    nPolyID,
    useOrigPointIds
  );

  Size polygonIDs[](1);
  polygonIDs[0] = polygonID;
  this.splitPolygon2ApplayTask(
    polygonIDs,
    nPolyID,
    polygonSizes,
    nPolygonSizes,
    packedPolygonPointIndices,
    nPackedPolygonPointIndices
  );

  pointIds = useOrigPointIds;
  for(Integer i=origPoiCnt; i<this.pointCount(); i++){
    pointIds.push(i);
  }
}

function PolygonMesh.splitPolygon2ArrayLoopTask!(
  in Size polygonID[],
  in Size polygonPointIndex1[],
  in Size origPoiCnt,
  in Scalar edgeSplitRatio1[],
  in Size polygonPointIndex2[],
  in Scalar edgeSplitRatio2[],
  in Vec3Array addPointsPosition[],
  in Boolean mergeNeighborPoly,
  io Vec3 preparePoiPos[],
  out Size holdOrigIDs[],
  out Vec3 holdSplitPos1[],
  out Vec3 holdSplitPos2[],
  io Size useOrigPointIds[]
  )
{
  if(polygonID.size() != polygonPointIndex1.size() ||
     polygonID.size() != edgeSplitRatio1.size() ||
     polygonID.size() != polygonPointIndex2.size() ||
     polygonID.size() != edgeSplitRatio2.size() 
     ){
    report("!!!!!!Error!!!!!! .splitPolygon2() ::: array size is not same");
    return;
  }

  Size polygonSizes[], 
       packedPolygonPointIndices[], 
       nPolygonSizes[], 
       nPackedPolygonPointIndices[], 
       dummy[],
       rest[],
       holdID[],
       execID[],
       execPolyID[];
  Integer nPolyID[];
  Vec3Array restAddPointsPosition[] = addPointsPosition.clone();
  if(addPointsPosition.size() == polygonID.size()){
    restAddPointsPosition.resize(polygonID.size());
  }

  //  同じポリゴンを分割しようとしている場合は２回目以降をholdIDに溜めて保留しておく
  for(Integer i=0; i<polygonID.size(); i++){
    Boolean same = false;
    for(Integer j=0; j<i; j++){
      if(polygonID[i] == polygonID[j]){
        same = true;
        j=i;
      }
    }
    if(same){
      rest.push(holdOrigIDs[i]);
      holdID.push(i);
    }else{
      execID.push(i);
      execPolyID.push(polygonID[i]);
    }
  }
  holdOrigIDs = rest;

  holdSplitPos1.resize(holdID.size());
  holdSplitPos2.resize(holdID.size());
  //  保留がある場合はエッジの分割座標を取得してholdSplitPosに溜めておく
  for(Integer i=0; i<holdID.size(); i++){
    Size id, id11, id12, id21, id22;
    id = holdID[i];
    id11 = this.getPolygonPoint(polygonID[id], polygonPointIndex1[id]);
    if(polygonPointIndex1[id] == this.getPolygonSize(polygonID[id])-1){
      id12 = this.getPolygonPoint(polygonID[id], 0);
    }else{
      id12 = this.getPolygonPoint(polygonID[id], polygonPointIndex1[id]+1);
    }
    holdSplitPos1[i] = this.getPointPosition(id11).linearInterpolate(this.getPointPosition(id12), edgeSplitRatio1[id]);

    id21 = this.getPolygonPoint(polygonID[id], polygonPointIndex2[id]);
    if(polygonPointIndex2[id] == this.getPolygonSize(polygonID[id])-1){
      id22 = this.getPolygonPoint(polygonID[id], 0);
    }else{
      id22 = this.getPolygonPoint(polygonID[id], polygonPointIndex2[id]+1);
    }
    holdSplitPos2[i] = this.getPointPosition(id21).linearInterpolate(this.getPointPosition(id22), edgeSplitRatio2[id]);
  }

  Size execPolyID2[](0);
  //  分割するすべてのポリゴンに対して必要な情報を集めていく
  for(Integer i=0; i<execID.size(); i++){
    Size id = execID[i];
    this.splitPolygon2GetValuesTask(
      execPolyID[i],
      execPolyID,
      origPoiCnt,
      polygonPointIndex1[id], 
      edgeSplitRatio1[id], 
      polygonPointIndex2[id], 
      edgeSplitRatio2[id], 
      restAddPointsPosition[id], 
      mergeNeighborPoly,
      execPolyID2,
      preparePoiPos,
      polygonSizes, 
      packedPolygonPointIndices, 
      nPolygonSizes, 
      nPackedPolygonPointIndices, 
      nPolyID,
      useOrigPointIds
    );
  }
  //  集めた情報を元にすべてのポリゴンに対して分割処理を実行する
  this.beginStructureChanges();
  {
    this.splitPolygon2ApplayTask(
      execPolyID2,
      nPolyID,
      polygonSizes,
      nPolygonSizes,
      packedPolygonPointIndices,
      nPackedPolygonPointIndices
    );
  }
  this.endStructureChanges();
}

function PolygonMesh.splitPolygon2Array!(
  in Size polygonID[],
  in Size polygonPointIndex1[],
  in Scalar edgeSplitRatio1[],
  in Size polygonPointIndex2[],
  in Scalar edgeSplitRatio2[],
  in Vec3Array addPointsPosition[],
  in Boolean mergeNeighborPoly,
  out Size usePointIds[],
  )
{
  Size polygonID_in[] = polygonID.clone();
  Size polygonPointIndex1_in[] = polygonPointIndex1.clone();
  Scalar edgeSplitRatio1_in[] = edgeSplitRatio1.clone();
  Size polygonPointIndex2_in[] = polygonPointIndex2.clone();
  Scalar edgeSplitRatio2_in[] = edgeSplitRatio2.clone();
  Vec3Array addPointsPosition_in[] = addPointsPosition.clone();

  Size origPoiCnt = this.pointCount();
  Vec3 holdSplitPos1[], holdSplitPos2[], preparePoiPos[];
  Size holdOrigIDs[](polygonID.size());
  for(Integer i=0; i<polygonID.size(); i++){
    holdOrigIDs[i] = i;
  }
  Boolean w = true;
  while(w){
    holdSplitPos1.resize(0);
    holdSplitPos2.resize(0);

    this.splitPolygon2ArrayLoopTask(
      polygonID_in, 
      polygonPointIndex1_in, 
      origPoiCnt,
      edgeSplitRatio1_in, 
      polygonPointIndex2_in,
      edgeSplitRatio2_in,
      addPointsPosition_in,
      mergeNeighborPoly,
      preparePoiPos,
      holdOrigIDs,
      holdSplitPos1,
      holdSplitPos2,
      usePointIds
    );

    if(holdOrigIDs.size() == 0){
      w = false;
    }else{  
      polygonID_in.resize(0);
      polygonPointIndex1_in.resize(0); 
      polygonPointIndex2_in.resize(0);
      edgeSplitRatio1_in.resize(0);
      edgeSplitRatio2_in.resize(0);
      addPointsPosition_in.resize(0);

      //  ポリゴン分割処理後の新しいメッシュに対してポリゴン（エッジ）IDや分割レシオを再計算する
      for(Integer i=0; i<holdOrigIDs.size(); i++){
        Size id = holdOrigIDs[i];
        
        Size polyID1, polyID2, polpoi1, polpoi2;
        Scalar ratio1, ratio2, dumS;
        Vec3 dumV;
        
        //  エッジ分割したかったポジションが新メッシュではどのポリゴン番号とエッジに所属するかを検索
        //  検索スタートポジションはエッジ上に存在するので、Lerp（）で少しだけ内側にずらして検索スタート
        GeometryLocation loc1, loc2;
        if(0<addPointsPosition[id].size()){
          loc1 = this.getClosest(holdSplitPos1[i].linearInterpolate(addPointsPosition[id][0], 0.001), Vec3(1.0), 1.0);
          loc2 = this.getClosest(holdSplitPos2[i].linearInterpolate(addPointsPosition[id][0], 0.001), Vec3(1.0), 1.0);
        }else{
          loc1 = this.getClosest(holdSplitPos1[i].linearInterpolate(holdSplitPos2[i], 0.001), Vec3(1.0), 1.0);
          loc2 = this.getClosest(holdSplitPos1[i].linearInterpolate(holdSplitPos2[i], 0.999), Vec3(1.0), 1.0);
        }
        if(loc1.isValid() && loc2.isValid()){
          this.getClosestEdgeFromLocation(loc1, polyID1, polpoi1, ratio1, dumS, dumV);
          this.getClosestEdgeFromLocation(loc2, polyID2, polpoi2, ratio2, dumS, dumV);
        }else{
          polyID1 = 0; polyID2 = 1;
        }

        if(polyID1 == polyID2){
          Size poi11, poi12, poi21, poi22;
          //  ↑で取得しているratioはLerpの際にずらしたポジション値からの結果なので使用できない
          //  本来のポジションんを使用し再計算する
          poi11 = this.getPolygonPoint(polyID1, polpoi1);
          if(polpoi1 == this.getPolygonSize(polyID1)-1){
            poi12 = this.getPolygonPoint(polyID1, 0);
          }else{
            poi12 = this.getPolygonPoint(polyID1, polpoi1+1);
          }
          ratio1 = this.getPointPosition(poi11).distanceTo(holdSplitPos1[i]) / this.getPointPosition(poi11).distanceTo( this.getPointPosition(poi12) );
          poi21 = this.getPolygonPoint(polyID2, polpoi2);
          if(polpoi2 == this.getPolygonSize(polyID2)-1){
            poi22 = this.getPolygonPoint(polyID2, 0);
          }else{
            poi22 = this.getPolygonPoint(polyID2, polpoi2+1);
          }
          ratio2 = this.getPointPosition(poi21).distanceTo(holdSplitPos2[i]) / this.getPointPosition(poi21).distanceTo( this.getPointPosition(poi22) );
        
          polygonID_in.push(polyID1);
          polygonPointIndex1_in.push(polpoi1); 
          polygonPointIndex2_in.push(polpoi2);
          edgeSplitRatio1_in.push(ratio1);
          edgeSplitRatio2_in.push(ratio2);
          addPointsPosition_in.push(addPointsPosition[id]);
        }else{
          report("!!!!!!SplitPolygon2::: split target polygon is not same one");
          w = false;
        }
      }
    }
  }//while() end

  for(Integer i=origPoiCnt; i<this.pointCount(); i++){
    usePointIds.push(i);
  }
}