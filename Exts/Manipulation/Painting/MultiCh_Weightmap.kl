//"2.4.0対策"　シーンロード時に何故かconvertToStringでセーブが走るため、ロードが必要なデータを改めてセーブしデータが保持されるように対応

require InlineDrawing;
require FileIO;
require JSON;
require FabricInterfaces;
require Manipulation;

object MultiChWeightmap : Weightmap, Persistable, RTValFromJSONDecoder, RTValToJSONEncoder{
  String MCWMapName;

  String chNames[];
  Size parentMapID[];
  Color colors[];

  MultiCh_SurfaceAttributePaintManipulator paintManipulator;

  Size chCount;
  Size oldChCount;
  Size activeCh;
  Size oldActiveCh;
  Size extMode; //0=normal, 1=SkinWeightPaint, 2=SkinWeightBlend

  Boolean weightLock[];

  private Boolean saveEnable;
  private Boolean saveExternalFile;
  private FilePath saveFolderPath;
  private String saveFileName;

  ScalarAttribute temporaryLoadAttr[];
  Boolean allChConnected;
  Boolean externalValuesConnected;
  Boolean colorSetted;
};

function MultiChWeightmap(){
  this.chNames.resize(1);
  this.chNames[0] = 'weightMap_ch0';
  this.colors.resize(1);
  this.colors[0] = mathRandomColor ( 10, 0, 0.05, 0.95 );
  this.chCount = 1;
  this.activeCh = 0;
  this.allChConnected = false;
  this.externalValuesConnected = false;
  this.extMode = 0;
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// prepare to channel
inline MultiChWeightmap.prepareCh!(Size chCount,  Color colors[]){
  if(chCount <= 0){
    setError("MultiChWeightmap node : input count of channel is "+chCount+"!!!!!");
    this.chCount = 1;
  }else{
    this.chCount = chCount;
  }
  if(this.chCount != this.oldChCount){
    this.allChConnected = false;
  }
  if(!this.allChConnected){
    if(chCount == colors.size()){
      this.chNames.resize(chCount);
      this.colors.resize(chCount);
      this.weightLock.resize(chCount);
      for(Integer i=0; i<chCount; i++){
        this.chNames[i] = 'weightMap_ch'+i;
        this.colors[i] = colors[i];
      }
    }else{
      setError("MultiChWeightmap node : count of channel is not same size fo colorArray!!  channel count = "+chCount+" : colors.size() = "+colors.size() );
    }
  }
  this.colors = colors;
}

inline MultiChWeightmap.prepareCh!(Size chCount){
  Color colors[];
  colors.resize(chCount);
  for(Integer i=0; i<chCount; i++){
    colors[i] = mathRandomColor ( i+10, i, 0.05, 0.95 );
  } 
  this.prepareCh(chCount, colors);
}

inline MultiChWeightmap.prepareCh!(Color colors[]){
  Size chCount = colors.size();
  this.prepareCh(chCount, colors);
}

inline MultiChWeightmap.setColors!(Color colors[]){
  if(this.chCount == colors.size()){
    this.prepareCh(this.chCount, colors);
  }else{
    setError("count of channel is not same size fo colorArray!!  channel count = "+this.chCount+" : colors.size() = "+colors.size() );
  }
}

inline MultiChWeightmap.changeActiveCh!(Size ch){
  if(this.chCount < ch){
    setError("");
  }
  this.oldActiveCh = this.activeCh;
  this.activeCh = ch;
}

// prepare to channel
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// output
inline String[] MultiChWeightmap.getChNames(){
  return this.chNames;
}

inline Scalar[][] MultiChWeightmap.getAllChValues?(Integer resize){
  Scalar values[][];
  values.resize(this.chNames.size());
  for(Integer i=0; i<this.chNames.size(); i++){
    Ref<ScalarAttribute> attr = this.mesh.getAttribute(this.chNames[i], ScalarAttribute);
    values[i] = attr.values.clone();
    if(resize){
      values[i].resize(this.mesh.pointCount());
    }
  }
  return values;
}

inline Scalar[] MultiChWeightmap.getMapValues?(Integer ch, Integer resize){
  Scalar values[];
  Ref<ScalarAttribute> attr = this.mesh.getAttribute(this.chNames[ch], ScalarAttribute);
  values = attr.values.clone();
  if(resize){
    values.resize(this.mesh.pointCount());
  }
  return values;
}

//　getIndices用のオペレータ　i=頂点or面の数
operator getIndicesTask<<<i>>>(
  io Boolean hasVal[], 
  in Integer mode, 
  in MultiChWeightmap map, 
  in String mapNames[], 
  in Integer logic, 
  in Scalar border
  ){
  Scalar sum = 0.0; // 各マップのペイント値の合計値の受け皿を準備
  Integer nonMapValPointCount = 0; // ポリゴン面を構成する各頂点のペイント値が存在するかどうかの判別用データを準備
  
  // "mode"が0なら頂点の各マップのペイント値の合計、1or2ならポリゴン面を構成する頂点の各マップのペイント値の合計平均値をsumで出力
  if(mode == 0){
    for(Integer j=0; j<mapNames.size(); j++){
      Ref<ScalarAttribute> attr = map.mesh.getAttribute(mapNames[j], ScalarAttribute);
      sum += attr.values[i];
    }
  }else{
    LocalL8UInt32Array points;
    map.mesh.getPolygonPoints(i, points); // ポリゴン面を構成する頂点番号を取得
    for(Integer j=0; j<points.count; j++){ // ポリゴン面を構成する頂点数だけループ処理
      Scalar poiSum = 0.0; // ポリゴン面の各頂点のペイント値を合計する受け皿を準備
      for(Integer k=0; k<mapNames.size(); k++){
        Ref<ScalarAttribute> attr = map.mesh.getAttribute(mapNames[k], ScalarAttribute);
        poiSum += attr.values[points.first8[j]];
      }
      if(poiSum == 0.0 && mode == 1){
        nonMapValPointCount += 1; // "mode"が1でペイント値の合計が0.0だった場合、以降の処理でfalseを戻すために+1を実行しておく
      }else{
        sum += poiSum; // "mode"が0か2の場合、次の処理でペイント値の平均値を出すためにsumにペイント値を合計しておく
      }
    }
    sum /= points.count; // ポリゴン面の各頂点のペイント合計値を頂点数で平均化
  }

  // "mode"が1で1つでもペイント値の無い頂点が存在する場合はfalse、それ以外はロジックに沿ってtrue or falseの結果を返す
  if(nonMapValPointCount != 0){
    hasVal[i] = false;
  }else{
    hasVal[i] = false;
    if(logic == 0){ if(sum >  border){  hasVal[i] = true; } }
    if(logic == 1){ if(sum >= border){  hasVal[i] = true; } }
    if(logic == 2){ if(sum == border){  hasVal[i] = true; } }
    if(logic == 3){ if(sum <= border){  hasVal[i] = true; } }
    if(logic == 4){ if(sum <  border){  hasVal[i] = true; } }
  }
}

//　ウェイトマップが塗られている頂点、ポリゴン面のIDを取得する
function Index[] MultiChWeightmap.getIndices?(
  in Integer mode, // 0:頂点　1:ポリゴン面（ポリゴンポイント全てが塗られている）　2:ポリゴン面（ポリゴンポイントで1つ以上が塗られている）
  in Integer mapAttrSelect,
  in String mapAttr_IDs,
  in String[] mapAttr_Names,
  in Integer logic,
  in Scalar border
  ){
  Index indices[]; // ID格納用データの準備　＝　戻り値

  // 検出対象にするマップ名のリストを作成する
  String mapNames[];
  switch(mapAttrSelect){
    case 0: // "mapAttr_IDs"に入力されたマップIDリストからマップアトリビュート名のリストを生成する
      String ch[];
      ch = mapAttr_IDs.split(',');
      mapNames.resize(ch.size());
      for(Integer i=0; i<ch.size(); i++){
        if(this.chNames[ ch[i].toScalar() ] == null){
          setError("mapAttrId "+ch[i]+" is nothing");
        }else{
          mapNames[i] = this.chNames[ ch[i].toScalar() ];
        }
      }
    case 1: // "mapAttr_Names"に入力されたマップアトリビュート名リストをそのまま使用
      mapNames = mapAttr_Names;
  }
  
  // 並列処理で行った検出結果を戻すために頂点orポリゴン面の数のリストを準備
  Boolean hasVal[];
  switch(mode){
    case 0:
      hasVal.resize(this.mesh.pointCount());
      break;
    case 1:
      hasVal.resize(this.mesh.polygonCount());
      break;
    case 2:
      hasVal.resize(this.mesh.polygonCount());
      break;
  }
  // 検出　上記オペレータを使用
  getIndicesTask<<<hasVal.size()>>>(hasVal, mode, this, mapNames, logic, border);
  
  // 検出結果からtrueのIDのみのリストを作成
  indices.resize(0);
  for(Integer i=0; i<hasVal.size(); i++){
    if(hasVal[i]){ indices.push(i); }
  }
  return indices;
}

//  GetMapValueOrderedListArray用のオペレータ　i=ロケーションの数
operator GetMapValueListArrayTask<<<i>>>(io Scalar values[][], PolygonMesh mesh, GeometryLocation locations[], String ch[]){
  GeometryAttributes attributes = mesh.attributes;
  for(Integer j=0; j<ch.size(); j++){
    GeometryAttribute attr = attributes.getAttribute(ch[j]);
    ScalarAttribute cast = attr;
    values[i][j] = GetAttributeAtLocation(mesh, locations[i], cast);
  }
}
//  ロケーションでのウェイトマップの値が大きなもの（小さなもの）を順番に取得する
//  取得の最大数は4
function GetMapValueOrderedListArray(
  in MultiChWeightmap multiChWeightmap,
  in PolygonMesh mesh,
  in GeometryLocation locations[],
  in Integer condition,
  in String ch[],
  in Integer maxCount,
  io LocalL4UInt32Array orderedIndex[],
  io LocalL4ScalarArray orderedMapValues[],
  io LocalL4ScalarArray weights[]
  ){
  //   入力されたデータの確認
  if(locations.size() == 0){
    return;
  }
  if(mesh == null){
    report("Error: missing geometry in \"GetValueAtLocation\"");
    return;
  }
  // ロケーションのマップ値を格納するための配列データを準備
  Scalar values[][];
  values.resize(locations.size());
  for(Integer i=0; i<locations.size(); i++){
    values[i].resize(ch.size());  
  }
  // ロケーションのマップ値を取得
  if(locations.size() < 128 ){
    for(Integer i=0; i<locations.size(); i++){
      GetMapValueListArrayTask(i, values, mesh, locations, ch);
    }
  }else{
    GetMapValueListArrayTask<<<locations.size()>>>(values, mesh, locations, ch);
  }
  // この後の昇順、降順の際にチャンネル番号も並び替えに連動するようにデータを準備
  Index targetID[][];
  targetID.resize(locations.size());
  for(Integer i=0; i<locations.size(); i++){
    targetID[i].resize(ch.size());
    for(Integer j=0; j<ch.size(); j++){
      targetID[i][j] = j;
    }
  }
  // 取得したマップ値を順に並べなおす
  for(Integer k=0; k<locations.size(); k++){
    Scalar tmpS;
    Index tmpID;
    for(Integer i=0; i<values[k].size(); i++){
      for(Integer j=i+1; j<values[k].size(); j++) {
        //  conditionが0の場合は降順、1の場合は昇順
        if(condition == 0){
          if(values[k][i] < values[k][j]) {
            tmpS =  values[k][i];
            tmpID = targetID[k][i];
            values[k][i] = values[k][j];
            targetID[k][i] = targetID[k][j];
            values[k][j] = tmpS;
            targetID[k][j] = tmpID;
          }
        }else{
          if(values[k][i] > values[k][j]) {
            tmpS =  values[k][i];
            tmpID = targetID[k][i];
            values[k][i] = values[k][j];
            targetID[k][i] = targetID[k][j];
            values[k][j] = tmpS;
            targetID[k][j] = tmpID;
          }
        }
      }
    }
  }
  // weights算出用にマップ値の合計値を計算しておく
  Scalar sum[];
  sum.resize(locations.size());
  Integer outPutCount[];
  outPutCount.resize(locations.size());
  for(Integer k=0; k<locations.size(); k++){
    for(Integer i=0; i<values[k].size(); i++){
      if(i < maxCount){
        sum[k] += values[k][i]; 
        outPutCount[k] += 1; 
      }else{
        break;
      }
    }
  }
  // 出力データ準備
  orderedIndex.resize(locations.size());
  orderedMapValues.resize(locations.size());
  weights.resize(locations.size());

  for(Integer k=0; k<locations.size(); k++){
    orderedIndex[k].resize(outPutCount[k]);
    orderedMapValues[k].resize(outPutCount[k]);
    weights[k].resize(outPutCount[k]);
    for(Integer i=0; i<outPutCount[k]; i++){
      orderedIndex[k].set(i, targetID[k][i]);
      orderedMapValues[k].set(i,values[k][i]);
      weights[k].set(i, values[k][i]/sum[k]); 
    }
  }
  //  MulWeightでターゲット数に不足がないかの確認を行えるようにターゲットの数を格納しておく
  //  カウントがothers[0]を作るために5になるが、MulWeightではweightListのcountを参照するので問題ないはず
  orderedIndex[0].count = 5;
  orderedIndex[0].others.resize(1);
  orderedIndex[0].others[0] = ch.size();
}

// output
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// brush
function MultiChWeightmap.setLimit!(Scalar min, Scalar max){
  this.paintManipulator.paintValLimit[0] = min;
  this.paintManipulator.paintValLimit[1] = max;
}

function MultiChWeightmap.lock!(Boolean weightLock[]){
  Boolean rest[] = weightLock;
  rest.resize( this.chCount );
  this.weightLock = weightLock;
}
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
operator setPointAttributeTask<<<i>>>(io PolygonMeshTopology mesh, io Scalar targetAttrVals[], Scalar setVals[]){
  mesh.setPointAttribute(i, targetAttrVals, setVals[i]);
}

inline MultiChWeightmap.connect!(PolygonMesh mesh, Xfo meshTransform){
  this.meshTransform = meshTransform;

  if(!this.allChConnected ){
    if(this.mesh == null){
      this.mesh = mesh;
    }
    if(!this.mesh.getAttributes().has('normals')){
      this.mesh.recomputePointNormals();
    }
    // Add the weightmaps for make to OGLSurfaceOverlayShader
    this.mesh.getOrCreateAttribute("weightMap", ScalarAttribute);
    // add ScalarAttributes
    for(Integer i=0; i<this.chCount; i++){
      if(this.temporaryLoadAttr.size() != 0 && this.chCount == this.oldChCount){
      // When loading from disk, the attribute was generated in the load function, 
      // and now we move to the mesh.(unless it already has)
        if(this.temporaryLoadAttr[i] != null ){
          Ref<ScalarAttribute> attr = this.mesh.getOrCreateAttribute(this.chNames[i], ScalarAttribute);
          setPointAttributeTask<<<this.mesh.pointCount()>>>(this.mesh.topology, attr.values, this.temporaryLoadAttr[i].values);
          this.temporaryLoadAttr[i] = null;
        }
      }
      else{
        this.mesh.getOrCreateAttribute(this.chNames[i], ScalarAttribute);
      }
    }

    if(this.instance){
      InlineTransform transform = this.instance.getTransform();
      if(transform)
      {
        transform.setLocalXfo(0, this.meshTransform);
      }
      InlineMeshShape shape = this.instance.getShape();
      if(shape)
      {
        shape.setMesh(this.mesh);
      }
    }
    this.allChConnected = true;
  }
  this.mesh.setAllPointPositions( mesh.getAllPointPositions() );
}

inline MultiChWeightmap.connect!(PolygonMesh mesh, Xfo meshTransform, Integer chCount){
  this.prepareCh(chCount);
  this.connect(mesh, meshTransform);
}

inline MultiChWeightmap.connect!(PolygonMesh mesh, Xfo meshTransform, Integer chCount, Color colors[]){
  this.prepareCh(chCount, colors);
  this.connect(mesh, meshTransform);
}

inline MultiChWeightmap.display!(Boolean display){
  if(!this.mesh)
    setError("Cannnot display the Weightmap without a geometry assigned");
  if(display){
    if(!this.handle){
      InlineDrawing drawing = OGLInlineDrawing_GetInstance();
      this.handle = DrawingHandle();
      InlineShader overlayShader = drawing.registerShader(OGLSurfaceOverlayShader(this.name));
      this.overlayMaterial = overlayShader.getOrCreateMaterial("weightMap_Mat");

      InlineMeshShape shape = InlineMeshShape(this.name+'Shape', this.mesh);
      drawing.registerShape(shape);
      InlineTransform transform = StaticInlineTransform(this.name+'Transform', this.handle.getRootTransform(), this.meshTransform);

      this.instance = InlineInstance(this.name+'Instance', transform, shape);
      this.instance.setMappedAttributeName("weightMap", this.chNames[this.activeCh]);
      this.instance.setInstanceUniform(InlineUniform('u_color', this.colors[this.activeCh]));
      this.instance.addMaterial(this.overlayMaterial); 
    }
    else {
      if(this.instance != null && !this.instance.hasMaterial(this.overlayMaterial)){
        this.instance.addMaterial(this.overlayMaterial); 
      }
      if(this.oldActiveCh != this.activeCh){
        this.instance.setMappedAttributeName("weightMap", this.chNames[this.activeCh]);
        this.instance.setInstanceUniform(InlineUniform('u_color', this.colors[this.activeCh]));
      }
    }
  }
  else {
    if(this.instance != null && this.instance.hasMaterial(this.overlayMaterial)){
      this.instance.removeMaterial(this.overlayMaterial); 
    }
  }
}

inline MultiChWeightmap.updateInstanceTransform(){
  this.instance.getTransform().setLocalXfo(0, this.meshTransform);
}

inline MultiChWeightmap.activateManipulator!(){
  if(!this.instance)
    this.display(true);
  if(!this.instance){
    setError("Cannnot activate the Weightmap without a geometry assigned");
  }
  Ref<EventDispatcher> eventDispatcher = EventDispatcher_GetInstance();
  if(this.activeCh < 0 || this.chCount <= this.activeCh){
    eventDispatcher.deactivateManipulation();
    setError("paintting channnel is over count of multiChannelWeightMap!!!!!! painting channel = "+this.activeCh+" : channnel count = "+this.chCount);
  }else{
    eventDispatcher.activateManipulation();
    this.display(true);
  }
  if(!this.paintManipulator){
    this.paintManipulator = MultiCh_SurfaceAttributePaintManipulator();
    this.paintManipulator.paintedChecker.resize(this.mesh.pointCount());
    eventDispatcher.registerManipulator(this.name+'PaintManipulator', this.paintManipulator);
    this.paintManipulator.valueDrawing = MultiCh_SurfaceAttributeValuesDrawing();
    this.paintManipulator.valueDrawing.connect(this.mesh, this.colors[this.activeCh]);
  }
  if(this.paintManipulator.attributeName != this.chNames[this.activeCh]){
    this.paintManipulator.setAttributeName(this.chNames[this.activeCh]);
    this.paintManipulator.activeCh = this.activeCh;
  }
  if(!this.paintManipulator.hasTargetGeometry(this.instance))
    this.paintManipulator.addTargetGeometry(this.instance);
  if(this.oldActiveCh != this.activeCh){
    for(Integer i=0; i<this.paintManipulator.normalizeChNum.size(); i++){
      this.paintManipulator.activeChIsNormalizeChAndLockEnable = false;
      if(this.activeCh == this.paintManipulator.normalizeChNum[i] || this.paintManipulator.weightLock[ this.paintManipulator.normalizeChNum[i] ]){
        this.paintManipulator.activeChIsNormalizeChAndLockEnable = true;
        break;
      }
    }
  }
  if(eventDispatcher.activeManipulator() != this.name+'PaintManipulator')
  {
    // activate the manipulator
    eventDispatcher.pushManipulator(this.name+'PaintManipulator');
    this.oldActiveCh = this.activeCh;
    this.oldChCount = this.chCount;
  }
  this.updateInstanceTransform();

  Vec3 pointPositions[] = this.mesh.getAllPointPositions();
  Ref<ScalarAttribute> attr = this.mesh.getAttribute(this.chNames[this.activeCh], ScalarAttribute);
  this.paintManipulator.valueDrawing.display(pointPositions, attr, this.colors[this.activeCh], this.meshTransform);
  this.instance.setInstanceUniform(InlineUniform('u_color', this.colors[this.activeCh]));
  this.paintManipulator.weightLock = this.weightLock;
  this.paintManipulator.chNames = this.chNames;
}
//  複数のマップバリューのノーマライズをON/OFFする
function MultiChWeightmap.normalize!(Boolean enable, Index normalizeChNum[]){
  //  既に登録されているノーマライズ対象チャンネル数が変更された場合に、ノーマライズに必要な情報を更新する
  if(enable && this.paintManipulator.normalizeChNum.size() != normalizeChNum.size() && normalizeChNum.size()!=0){
    this.paintManipulator.normalizeChNum = normalizeChNum;
    this.paintManipulator.IDs.resize(this.mesh.pointCount());
    this.paintManipulator.Weights.resize(this.mesh.pointCount());
  }
  //  アクティブチャンネルがノーマライズ対象であればノーマライズ処理を有効にする
  for(Integer i=0; i<normalizeChNum.size(); i++){
    this.paintManipulator.normalize = false;
    if(this.activeCh == normalizeChNum[i]){
      this.paintManipulator.normalize = enable;
      break;
    }
  }
}
//  ノーマライズを行うチャンネルをBooleanの配列で指定する場合に使用
//  Boolean[]でtrueだった配列番号（＝チャンネル番号）をnormalizeChNumに格納し↑のメソッドへ投げる
function MultiChWeightmap.normalize!(Boolean enable, Boolean normalizeCh[]){
  Size count = normalizeCh.size();
  if(this.chNames.size() < normalizeCh.size()){
    count = this.chNames.size();
  }
  Index normalizeChNum[];
  for(Integer i=0; i<count; i++){
    if(normalizeCh[i]){
      normalizeChNum.push(i);
    } 
  }
  this.normalize(enable, normalizeChNum);
}
//  全てのチャンネルに対してノーマライズをOn/Offする場合に使用
//  全てのチャンネル番号をnormalizeChNumに格納し↑のメソッドへ投げる
function MultiChWeightmap.normalize!(Boolean enable){
  Index normalizeChNum[];
  normalizeChNum.resize(this.chNames.size());
  for(Integer i=0; i<this.chNames.size(); i++){
    normalizeChNum[i] = i;
  }
  this.normalize(enable, normalizeChNum);
}
//  指定したチャンネルに値を設定する
function MultiChWeightmap.setValues!(io PolygonMesh mesh, Index setChannel, Scalar values[]){
  if(this.chCount < setChannel){
    setError("MultiChWeightmap.setValues node : setChannel is range out map channel count." + "setChannel = "+setChannel+"channel count = "+this.chCount);
  }else if(mesh.pointCount() != values.size()){
    setError("MultiChWeightmap.setValues node : not same!! mesh point count is "+mesh.pointCount()+" : values count is "+values.size());
  }else{
    this.oldChCount = this.chCount;
    //  マップバリューの受け渡し場所の.temporaryLoadAttrがチャンネル数と同じだけ存在しない場合に準備し、その後一旦nullにしておく
    if(this.temporaryLoadAttr.size() != this.chCount){
      this.temporaryLoadAttr.resize(this.chCount);
      for(Integer i=0; i<this.chCount; i++){
        this.temporaryLoadAttr[i] = ScalarAttribute();
        this.temporaryLoadAttr[i] = null;
      }
    }
    //  指定チャンネルへのバリュー受け渡し場所にデータを入れる
    this.temporaryLoadAttr[setChannel] = ScalarAttribute();
    this.temporaryLoadAttr[setChannel].values = values;
    //  渡したバリューをウェイトマップへ再コネクト
    this.allChConnected = false;
    this.connect(mesh, this.meshTransform);
  }
}
// 
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// save & load

inline MultiChWeightmap.save!(Boolean saveEnable, Boolean saveExternalFile, FilePath saveFolderPath, String saveFileName){
  this.saveEnable = saveEnable;
  this.saveExternalFile = saveExternalFile;
  this.saveFolderPath = saveFolderPath;
  this.saveFileName = saveFileName;
}

/*
inline MultiChWeightmap.save!(Boolean saveEnable, Boolean saveExternalFile){
  this.saveEnable = saveEnable;
  this.saveExternalFile = saveExternalFile;
}*/

inline JSONDictValue MultiChWeightmap.saveBin(){
  JSONDictValue root();
  if(!this.saveFolderPath.exists()){
    setError("!!!!!!!!!!!!!!!! Could not save !!!!!!!!!!!!!! saveFolderPath is not exists : " + this.saveFolderPath.string() );
  }else if(!this.saveFileName){
    setError("!!!!!!!!!!!!!!!! Could not save !!!!!!!!!!!!!! saveFileName is null");
  }else{
    // prepare save file path & file name
    FilePath binCacheFilePath(); 
    binCacheFilePath = this.saveFolderPath / FilePath(this.saveFileName + '_' + this.name + 'BinCache.bin');

    BinaryBlockWriter blockWriter(binCacheFilePath.string());
    blockWriter.setNumBlocks(2);
    BinaryBlockWriter mapChNames = blockWriter.beginWriteBlock('channelNames', this.chNames.size());{
      for(Integer i=0; i<this.chNames.size(); i++){
        BinaryBlockWriter mapChNameSub = mapChNames.beginWriteBlock( 'name' + i ); {
          UInt32 numElements = this.chNames[i].length();
          mapChNameSub.write(numElements.data(), numElements.dataSize());
          mapChNameSub.write(this.chNames[i].data(), this.chNames[i].dataSize() );
        }
      }
    }
    BinaryBlockWriter mapChValues = blockWriter.beginWriteBlock('mapValues', this.chCount);{
      for(Integer i=0; i<this.chCount; i++){
        BinaryBlockWriter mapChValSub = mapChValues.beginWriteBlock( 'val' + i );{
          UInt32 numElements;
          ScalarAttribute mapValData();
          //2.4.0対策
            if(this.mesh == null){
              mapValData = this.temporaryLoadAttr[i];
              numElements = this.temporaryLoadAttr[i].size();
            }
          else{
            mapValData = this.mesh.getAttribute(this.chNames[i], ScalarAttribute);
            numElements = this.mesh.pointCount();
          }
          Scalar data[] = mapValData.values;
          mapChValSub.write(numElements.data(), numElements.dataSize());
          mapChValSub.write(data.data(), data.dataSize());
        }
      }
    }
    report('MultiWeightMap value data save to "' + binCacheFilePath.string() +'"');
    root.setString("saveFolderPath", binCacheFilePath.removeFileName().string() );
    root.setString("saveFileName", this.saveFileName + '_' + this.name + 'BinCache.bin');
    root.setString("saveFileNameOrig", this.saveFileName);
  }
  return root;
}

inline JSONDictValue MultiChWeightmap.saveJSON(){
  JSONDictValue root();
  // JSON write
  JSONArrayValue channelNames();{
    channelNames.values.resize(this.chNames.size());
    for(Integer ii=0; ii<this.chNames.size(); ii++){
      channelNames.values[ii] = JSONStringValue(this.chNames[ii]);
    }
  }
  JSONArrayValue mapValues(); {
    for(Integer i=0; i<this.chCount; i++){
      JSONArrayValue mapValue();
      ScalarAttribute mapValData();
      //2.4.0対策
        if(this.mesh == null){
          mapValData = this.temporaryLoadAttr[i];
        }else{
          mapValData = this.mesh.getAttribute(this.chNames[i], ScalarAttribute);
        }
      mapValue.values.resize(mapValData.values.size());
      for(Integer ii=0; ii<mapValData.values.size(); ii++){
        mapValue.values[ii] = JSONNumberValue( mapValData.values[ii] );
      }
      mapValues.add(mapValue);
    }
  }
  root.set("channelNames", channelNames);
  root.set("mapValues", mapValues);
  report("save weightMaps in this scean");
  return root;
}

inline MultiChWeightmap.loadBinFilePath!(FilePath binCacheFilePath){
  BinaryBlockReader blockReader(binCacheFilePath.string());
  BinaryBlockReader mapChNames = blockReader.beginReadBlock('channelNames');
  if(mapChNames){
    String readNames[] = mapChNames.getBlockNames();
    this.chCount = readNames.size();
    this.chNames.resize(readNames.size());
    for(Integer i=0; i < readNames.size(); i++){
      BinaryBlockReader MapChName_reader = mapChNames.beginReadBlock('name' + i );
      UInt32 numElements = 0.0;
      MapChName_reader.read(numElements.data(), numElements.dataSize());
      for(Integer ii=0; ii<numElements; ii++){
        this.chNames[i] += ' ';
      }
      MapChName_reader.read(this.chNames[i].data(), this.chNames[i].dataSize());
    }
  }
  BinaryBlockReader mapChValues = blockReader.beginReadBlock('mapValues');
  if(mapChValues){   
    Scalar readVals[] = mapChValues.getBlockNames().size();
    this.temporaryLoadAttr.resize(readVals.size());
    for(Integer i=0; i<readVals.size(); i++){
      BinaryBlockReader mapChValuesSub = mapChValues.beginReadBlock('val' + i );
      SInt32 numElements = 0;
      mapChValuesSub.read(numElements.data(), numElements.dataSize());
      this.temporaryLoadAttr[i] = ScalarAttribute();
      this.temporaryLoadAttr[i].name = this.chNames[i];
      this.temporaryLoadAttr[i].resize(numElements);
      mapChValuesSub.read(this.temporaryLoadAttr[i].values.data(), this.temporaryLoadAttr[i].values.dataSize());
    }
  }
  report('MultiWeightMap value data load from "' + binCacheFilePath.string() +'"');
  this.allChConnected = false;
}

inline MultiChWeightmap.loadJSON!(JSONDictValue json){
  JSONArrayValue channelNames = json.get("channelNames");
  this.chNames.resize(channelNames.size());
  for(Integer i=0; i<channelNames.size(); i++){
    this.chNames[i] = channelNames.getString(i);
  }
  JSONArrayValue mapValues = json.get("mapValues");
  this.temporaryLoadAttr.resize(mapValues.size());
  for(Integer i=0; i<mapValues.size(); i++){
    JSONArrayValue mapValue = mapValues.get(i);
    this.temporaryLoadAttr[i] = ScalarAttribute();
    this.temporaryLoadAttr[i].name = this.chNames[i];
    this.temporaryLoadAttr[i].resize(mapValue.size());
    for(Integer ii=0; ii<mapValue.size(); ii++){
      this.temporaryLoadAttr[i].values[ii] = mapValue.getScalar(ii);
    }
  }
}

function String MultiChWeightmap.saveDataToString(PersistenceContext persistenceContext){
  JSONDoc doc();
  if(this.saveEnable){
    if(this.saveExternalFile){
      // save bin 
      doc.root = this.saveBin();
    }else{
      // save JSON
      doc.root = this.saveJSON();
    }
    doc.root.setBoolean("saved", this.saveEnable);
    doc.root.setInteger("chCount", this.chCount);
    doc.root.setString("name", this.name);
  }
  String json = doc.write();
  return json;
}

function MultiChWeightmap.loadDataFromString!(PersistenceContext persistenceContext, String data){
  JSONDoc doc();
  if(doc.parse(data)){
    if(doc.root.has("saved")){
      this.name = doc.root.getString("name");
      this.chCount = doc.root.getInteger("chCount");
      this.oldChCount = this.chCount;
      this.allChConnected = false;
      this.externalValuesConnected = true;
      this.saveEnable = true;
      // load bin
      if(doc.root.has("saveFolderPath") && doc.root.has("saveFileName")){
        this.saveFolderPath = FilePath(doc.root.getString("saveFolderPath"));
        String loadFileName = doc.root.getString("saveFileName");
        FilePath loadBinFilePath = this.saveFolderPath / FilePath(loadFileName) ;
        if(loadBinFilePath.exists()){
          this.loadBinFilePath(loadBinFilePath);
        }else{
          report("ERROR Loading MultiChWeightmap data file. File not found from ----" + loadBinFilePath.string());
        }
      }
      // load JSON
      if(doc.root.has("mapValues")){
        this.loadJSON(doc.root);
      }
    }
  }
}

function String MultiChWeightmap.convertToString?(){
  JSONDoc doc();
  if(this.saveEnable){
    if(this.saveExternalFile){
      // save bin
      doc.root = this.saveBin();
    }else{
      // save JSON
      doc.root = this.saveJSON();
    }
    doc.root.setBoolean("saved", this.saveEnable);
    doc.root.setInteger("chCount", this.chCount);
    doc.root.setString("name", this.name);
    doc.root.setBoolean("saved"+this.MCWMapName, this.saveEnable);
  }
  String json = doc.write();
  return json;
}

function Boolean MultiChWeightmap.convertFromString!(in String data){
  JSONDoc doc();
  if(doc.parse(data)){
    if(doc.root.has("saved"+this.MCWMapName)){
      this.name = doc.root.getString("name");
      this.chCount = doc.root.getInteger("chCount");
      this.oldChCount = this.chCount;
      this.externalValuesConnected = true;
      this.saveEnable = true;
      // load bin
      if(doc.root.has("saveFolderPath") && doc.root.has("saveFileName")){
        this.saveExternalFile = true;
        this.saveFolderPath = FilePath(doc.root.getString("saveFolderPath"));
        this.saveFileName = doc.root.getString("saveFileNameOrig"); 
        String loadFileName = doc.root.getString("saveFileName");
        FilePath loadBinFilePath = this.saveFolderPath / FilePath(loadFileName) ;
        if(loadBinFilePath.exists()){
          this.loadBinFilePath(loadBinFilePath);
        }else{
          report("!!!!!!!!!!!!!!!! can not load MultiChWeightmap data file. File don't found from ----" + loadBinFilePath.string());
        }
      }
      // load JSON
      if(doc.root.has("mapValues")){
        this.loadJSON(doc.root);
      }
    }
  }
  return true;
}